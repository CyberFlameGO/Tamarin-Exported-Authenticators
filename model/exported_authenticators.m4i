include(crypto.m4i)dnl
include(msgs.m4i)dnl
include(adversary.m4i)dnl

rule Register_pk:
    [ Fr(~ltk) ]
  -->
    [ !Ltk($A, ~ltk), !Pk($A, pk(~ltk)), DelegateLtk($TP, ~ltk) ]


rule Get_pk:
    [ !Pk(A, pubkey) ]
  -->
     [ Out(pubkey) ]

rule Create_TLS_channel_bi:
    [ Fr(ms)
    , Fr(TLS_transcript) 
    , Fr(cid)
    , Create_TLS_Channel(owner_host, owner_cid, target_host)
    ]
  --[
      SessionKey(ms, TLS_transcript)
    ]->
    [ !TLS_Channel(ms, TLS_transcript, 'bi', owner_host, owner_cid, target_host, cid)
    , !Exporters(ms, 'server',  cid, Handshake_Context(client), Handshake_Context(server), Finished_MAC(client), Finished_MAC(server))
    , ServerStart(cid)
    , !Exporters(ms, 'client', owner_cid, Handshake_Context(client), Handshake_Context(server), Finished_MAC(client), Finished_MAC(server))
    ]

rule Create_TLS_channel_uni:
    [ Fr(ms)
    , Fr(TLS_transcript) 
    , Fr(cid)
    , Create_TLS_Channel(owner_host, owner_cid, target_host)
    ]
  --[
      SessionKey(ms, TLS_transcript)
    ]->
    [ !TLS_Channel(ms, TLS_transcript, 'uni', 'place_holder', owner_cid, target_host, cid)
    , !Exporters(ms, 'server',  cid, Handshake_Context(client), Handshake_Context(server), Finished_MAC(client), Finished_MAC(server))
    , ServerStart(cid)
    , !Exporters(ms, 'client', owner_cid, Handshake_Context(client), Handshake_Context(server), Finished_MAC(client), Finished_MAC(server))
    ]

rule Send_TLS_Channel_owner_bi:
    [ TLS_Send(cid, from_host, to_host, msg)
    , !TLS_Channel(ms, TLS_transcript, 'bi', owner_host, owner_cid, target_host, target_cid)
    ]
  --[ Check_pair_or_swapped(from_host, to_host, owner_host, target_host)
    , Eq(cid, owner_cid)
    ]->
    [ TLS_Recv(target_cid, from_host, to_host, msg) ]

rule Send_TLS_Channel_owner_uni:
    [ TLS_Send(cid, from_host, to_host, msg)
    , !TLS_Channel(ms, TLS_transcript, 'uni', owner_host, owner_cid, target_host, target_cid)
    ]
  --[ Eq(to_host, target_host)
    , Eq(cid, owner_cid)
    ]->
    [ TLS_Recv(target_cid, owner_host, to_host, msg) ]

rule Send_TLS_Channel_target:
    [ TLS_Send(cid, from_host, to_host, msg)
    , !TLS_Channel(ms, TLS_transcript, auth_status, owner_host, owner_cid, target_host, target_cid)
    ]
  --[ Check_pair_or_swapped(from_host, to_host, owner_host, target_host)
    , Eq(cid, target_cid)
    ]->
    [ TLS_Recv(owner_cid, from_host, to_host, msg) ]

/* Protocol */
rule C_0:
  let
    C=$C
    S=$S
  in
    [ Fr(~tid)
    ]
  --[ Start(~tid, C, S, 'client') 
    , C0(~tid)
    ]->
    [ State(~tid, 'C0', C, S, 'client')
    , Create_TLS_Channel(C, ~tid, S)
    ]

rule C_Send:
  let
    C=$C
    S=$S
    certificate_request_context=~certificate_request_context
    certificate=pk(~ltkA)
    signature=compute_signature(~ltkA, h(hccc, certificate))
    verify_data = hmac(fmc, <hccc, Certificate, CertificateVerify>)
  in
    [ State(~tid, 'C0', C, S, 'client')
    , Fr(~certificate_request_context)
    , !Exporters(~ms, 'client', ~tid, hccc, hccs, fmc, fms)
    , !Ltk($A, ~ltkA)
    , DelegateLtk($C, ~ltkA)
    ]
  --[ C_Send(~tid)
    , Owns($C, <$A, ~ltkA>)
    , Instance(~tid, C, S, 'client')
    ]->
    [ State(~tid, 'C0', C, S, 'client')
    , TLS_Send(~tid, C, S, <Certificate, CertificateVerify, Finished>) 
    ]
    
rule C_Recv:
  let 
    C=$C
    S=$S
    certificate=pk(~ltkA)
    sig_msg=signature_input(h(hccs, certificate))
    exp_verify_data = hmac(fms, <hccs, Certificate, CertificateVerify>)
  in
    [ State(~tid, 'C0', C, S, 'client')
    , TLS_Recv(~tid, $PH, S, <Certificate, CertificateVerify, Finished>)
    , !Exporters(~ms, 'client', ~tid, hccc, hccs, fmc, fms)
    , !Pk($A, pk(~ltkA))
    ]
  --[ C_Recv(~tid)
    , Eq(verify(signature, sig_msg, pk(~ltkA)), true)
    , Eq(verify_data, exp_verify_data)
    , Recv(C, S, <$A, pk(~ltkA)>)
    , Instance(~tid, C, S, 'client')
    ]->
    [ State(~tid, 'C0', C, S, 'client')
    , !AcceptOwnership(~tid, C, S, pk(~ltkA))
    ]

rule S_0:
  let
    C=$C
    S=$S
  in
    [ !Exporters(~ms, 'server', ~tid, hccc, hccs, fmc, fms)
    , ServerStart(~tid)
    ]
  --[ Start(~tid, S, C, 'server')
    , S0(~tid)
    ]->
    [ State(~tid, 'S0', S, C, 'server')
    ]

rule S_Recv:
  let 
    C=$C
    S=$S
    certificate=pk(~ltkA)
    sig_msg=signature_input(h(hccc, certificate))
    exp_verify_data = hmac(fmc, <hccc, Certificate, CertificateVerify>)
  in
    [ State(~tid, 'S0', S, C, 'server')
    , TLS_Recv(~tid, C, S, <Certificate, CertificateVerify, Finished>)
    , !Exporters(~ms, 'server', ~tid, hccc, hccs, fmc, fms)
    , !Pk($A, pk(~ltkA))
    ]
  --[ S_Recv(~tid)
    , Eq(verify(signature, sig_msg, pk(~ltkA)), true)
    , Eq(verify_data,exp_verify_data)
    , Recv(S, C, <$A, pk(~ltkA)>)
    , Instance(~tid, S, C, 'server')
    ]->
    [ State(~tid, 'S0', S, C, 'server')
    , !AcceptOwnership(~tid, C, S, pk(~ltkA))
    ]

rule S_Send:
  let
    C=$C
    S=$S
    certificate_request_context=~certificate_request_context
    certificate=pk(~ltkA)
    signature=compute_signature(~ltkA, h(hccs, certificate))
    verify_data = hmac(fms, <hccs, Certificate, CertificateVerify>)
  in
    [ State(~tid, 'S0', S, C, 'server')
    , Fr(~certificate_request_context)
    , !Exporters(~ms, 'server', ~tid, hccc, hccs, fmc, fms)
    , !Ltk($A, ~ltkA)
    , DelegateLtk($S, ~ltkA)  
    ]
  --[ S_Send(~tid)
    , Owns($S, <$A, ~ltkA>)
    , Instance(~tid, S, C, 'server')
    ]->
    [ State(~tid, 'S0', S, C, 'server')
    , TLS_Send(~tid, C, S, <Certificate, CertificateVerify, Finished>)
    ]
